load d.'/gnuplot/_anim'
load d.'/gnuplot/_styles'

set size ratio -1 # scale 1:1

set nokey
set nomouse
set macros

r = 8

set xrange [-r:r]
set yrange [-r:r]

t = 0
T = 10000 # Should match numFrames
dt = 2
pauseTime = 0.01

# 'every 1::t::t' plots only the 't'th point.
# 'every 10::0::t' plots every 10th point from 0 to the 't'th point.

point = "every 1::t::t u 2:3 w p"
accel = "every 1::t::t u 2:3:6:7 w vectors"
trajectory = "every 1::(t > 10 ? t - 10 : 0)::t u 2:3 w l"

realt = system("cat system | grep -v '^#' 2>/dev/null | sed '".t."q;d' | cut -f1")
set title "t = ".realt

plot 'sun'     @point ls 1, \
     'jupiter' @point ls 2, \
     'jupiter' @accel ls 2, \
     'jupiter' @trajectory ls 2, \
     'trojans' @point ls 3, \
     'trojans' @accel ls 3, \
     'trojans' @trajectory ls 3, \
     'greeks'  @point ls 4, \
     'greeks'  @accel ls 4, \
     'greeks'  @trajectory ls 4

load d.'/gnuplot/_timestep'
